plugins {
  id 'application'
  id 'checkstyle'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'org.springframework.boot' version '2.4.5'
  id 'org.owasp.dependencycheck' version '6.2.0'
  id 'com.github.ben-manes.versions' version '0.39.0'
  id 'org.sonarqube' version '3.2.0'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

allprojects {
  sourceCompatibility = '11'
  targetCompatibility = '11'
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }

  acceptanceTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/acceptanceTest/java')
    }
    resources.srcDir file('src/acceptanceTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  include "uk/gov/hmcts/futurehearings/pip/functional/**"
  testLogging.showStandardStreams = true
  systemProperties System.getProperties()
  environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
  enableAssertions = true
  scanForTestClasses = true
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
  include "uk/gov/hmcts/futurehearings/pip/smoke/**"
  testLogging.showStandardStreams = true
  systemProperties System.getProperties()
  environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
  enableAssertions = true
  scanForTestClasses = true
}

task acceptance(type: Test) {
  description = 'Runs the QA Acceptance tests'
  group = 'Delivery pipeline'
  useJUnitPlatform()
  testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
  classpath = sourceSets.acceptanceTest.runtimeClasspath
  include "uk/gov/hmcts/futurehearings/pip/acceptance/**"
  testLogging.showStandardStreams = true
  systemProperties System.getProperties()
  environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
  enableAssertions = true
  scanForTestClasses = true
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.29'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
  properties {
    property "sonar.projectName", "PIP :: Publishing Management Interface API"
    property "sonar.projectKey", "PIPAPI"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.organization", "hmcts"
    property "sonar.sources", "src/main/java,pipeline,infrastructure"
    property "sonar.tests", "src/test/java,src/acceptanceTest/java,src/functionalTest/java"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

dependencyManagement {
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.68'
    // CVE-2018-10237 - Unbounded memory allocation
    dependencySet(group: 'com.google.guava', version: '30.1.1-jre') {
      entry 'guava'
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

def versions = [
  junit           : '5.7.2',
  junitPlatform   : '1.7.2',
  reformLogging   : '5.1.7',
  springBoot      : springBoot.class.package.implementationVersion,
  springfoxSwagger: '3.0.0'
]

ext.libraries = [
  junit5: [
    "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    "org.junit.jupiter:junit-jupiter-params:${versions.junit}",
    "org.junit.platform:junit-platform-commons:${versions.junitPlatform}",
    "org.junit.platform:junit-platform-engine:${versions.junitPlatform}"
  ]
]

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'

  implementation group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger

  implementation group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformLogging
  implementation group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformLogging
  //implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.8.RELEASE'

  compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
  compile group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.14.1'

  compile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '10.0.6'
  compile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-websocket', version: '10.0.6'

  annotationProcessor 'org.projectlombok:lombok:1.18.12'
  compileOnly 'org.projectlombok:lombok:1.18.12'
  testImplementation "ch.qos.logback:logback-classic:1.2.3"
  testImplementation group: 'com.aventstack', name: 'extentreports', version: '5.0.2'

  testImplementation libraries.junit5
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }

  testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
  testImplementation group: 'io.rest-assured', name: 'json-schema-validator', version: versions.restAssured
  testImplementation group: 'io.rest-assured', name: 'xml-path', version: versions.restAssured
  testImplementation group: 'io.rest-assured', name: 'json-path', version: versions.restAssured

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath

  acceptanceTestAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
  acceptanceTestImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
  acceptanceTestImplementation group: 'io.rest-assured', name: 'json-schema-validator', version: versions.restAssured
  acceptanceTestImplementation group: 'io.rest-assured', name: 'xml-path', version: versions.restAssured
  acceptanceTestImplementation group: 'io.rest-assured', name: 'json-path', version: versions.restAssured

  acceptanceTestImplementation sourceSets.main.runtimeClasspath
  acceptanceTestImplementation sourceSets.test.runtimeClasspath

  functionalTestAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
  functionalTestImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
  functionalTestImplementation group: 'io.rest-assured', name: 'json-schema-validator', version: versions.restAssured
  functionalTestImplementation group: 'io.rest-assured', name: 'xml-path', version: versions.restAssured
  functionalTestImplementation group: 'io.rest-assured', name: 'json-path', version: versions.restAssured

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath

  smokeTestImplementation sourceSets.main.runtimeClasspath
  smokeTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter', {
    exclude group: 'org.hamcrest', module: 'hamcrest-core'
  }
  smokeTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter', {
    exclude group: 'org.hamcrest', module: 'hamcrest-core'
  }
  smokeTestCompile("org.springframework.boot:spring-boot-starter-test")
  smokeTestAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
  smokeTestCompileOnly 'org.projectlombok:lombok:1.18.12'
  smokeTestImplementation 'org.junit.jupiter:junit-jupiter-api:' + versions.junit
  smokeTestImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
}

mainClassName = 'uk.gov.hmcts.reform.demo.Application'

bootJar {
  getArchiveFileName().set(provider {
    'spring-boot-template.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
